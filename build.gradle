import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import java.time.ZoneOffset
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

def buildDate = ZonedDateTime.now(ZoneOffset.UTC)
version = String.format("%d.%d.%d-%d", buildDate.year, buildDate.monthValue, buildDate.dayOfMonth, buildDate.toLocalTime().toSecondOfDay())

def os = OperatingSystem.current()
def userHome = System.getProperty('user.home')

ext {
    isWindows = os.isWindows()
    isMac     = os.isMacOsX()
    isLinux   = os.isLinux()

    hytaleHome = 
        isWindows ? "${userHome}/AppData/Roaming/Hytale" :
        isMac     ? "${userHome}/Library/Application Support/Hytale" :
                    "${userHome}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"

    serverJar    = "${hytaleHome}/install/${patchline}/package/game/latest/Server/HytaleServer.jar"
    modsFolder   = "${hytaleHome}/UserData/Mods"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code. This requires you to have Hytale installed using
// the official launcher for now.
dependencies {
    implementation(files(serverJar))
    compileOnly(files("libs/MultipleHUD-1.0.1.jar"))
}

// Updates the manifest.json file with the latest properties defined in the
// build.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

def pluginJarPrefix = "${project.name}-"
tasks.register('copyPlugin', Copy) {
    dependsOn 'jar'

    doFirst {
        file(modsFolder).listFiles()?.each { file ->
            if (file.name.startsWith(pluginJarPrefix) && file.name.endsWith('.jar')) {
                println "Deleted old plugin: ${file.name} from ${modsFolder}"
                file.delete()
            }
        }
    }

    from(tasks.named('jar'))
    into modsFolder

    doLast {
        println "Copied latest plugin to: ${modsFolder}"
    }
}

build.finalizedBy 'copyPlugin'
